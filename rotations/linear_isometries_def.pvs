%----------------------------------------------------------------------------
% Linear Isometries Definitions
% Authors: Gustavo Ferreira dos Santos
% 	   Universidade Federal de Goiás - Brazil
%
%	   Thaynara Arielly de Lima
%	   Universidade Federal de Goiás - Brazil
%
%---------------------------------------------------------------------------
% This theory contains the basic definitions for isometries on R^n.
%----------------------------------------------------------------------------
linear_isometries_def[n: posnat]: THEORY

BEGIN

  IMPORTING 
    matrices@matrices,
    matrices@norms,
    structures@list_lemmas

  % Vectors are represented as n x 1 matrices (Column_Matrix)

  ConsVecN : TYPE = {v: VectorN(n)| cons?(v)}
  Function_n: TYPE = [ConsVecN -> ConsVecN]
  
  % Variables
  f, g: VAR Function_n
  x, y: VAR  ConsVecN
  norm: VAR (norm?(n))
  a: VAR real
  
  %--------------------------------------------------------------------------
  % Definition 1.1: Isometry
  % An isometry of R^n is a function f: R^n -> R^n such that for any two
  % vectors x, y in R^n, we have |f(x) - f(y)| = |x - y|.
  %--------------------------------------------------------------------------

  add_ConsVecN: JUDGEMENT
    x + y HAS_TYPE ConsVecN

  isometry?(f)(norm): bool = 
    FORALL (x, y): norm(f(x) - f(y)) = norm(x - y)

  %--------------------------------------------------------------------------
  % Helper predicates
  %--------------------------------------------------------------------------
  
  % A function fixes the origin if f(0) = 0
   fixes_origin?(f): bool = 
     f(zero(n)) = zero(n)

  %A function preserves lengths if |f(x)| = |x| for all x
  preserves_length?(f)(norm): bool = 
    FORALL (x): norm(f(x)) = norm(x)

  % A function is linear
  % Why do we need the notion of linear function at this point?
  % Should we investigate if there is another one in Linear Algebra?
  
  linear?(f): bool = 
    (FORALL (x, y, a): (f(x + y) = f(x) + f(y)) AND f(a * x) = a * f(x))

  % linear_isometry?(f) is an isometry that fixes the origin.
  % linear_isometries will be characterize as linear transformations. 
  
  linear_isometry?(f)(norm): bool = 
    isometry?(f)(norm) AND fixes_origin?(f)

END linear_isometries_def
